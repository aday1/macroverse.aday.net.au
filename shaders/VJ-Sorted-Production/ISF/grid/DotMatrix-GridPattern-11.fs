/*{
    "DESCRIPTION": "DotMatrix-GridPattern-11",
    "CREDIT": "GLSL Sandbox / various",
    "ISFVSN": "2.0",
    "CATEGORIES": [
        "grid"
    ],
    "INPUTS": [
        {
            "NAME": "useFrameIndex",
            "TYPE": "bool",
            "DEFAULT": 0,
            "LABEL": "Use frame index (timeline sync)"
        },
        {
            "NAME": "fps",
            "TYPE": "float",
            "DEFAULT": 60.0,
            "MIN": 24.0,
            "MAX": 120.0
        },
        {
            "NAME": "timeScale",
            "TYPE": "float",
            "DEFAULT": 1.0,
            "MIN": 0.1,
            "MAX": 4.0,
            "LABEL": "Time speed"
        },
        {
            "NAME": "mouseX",
            "TYPE": "float",
            "DEFAULT": 0.5,
            "MIN": 0.0,
            "MAX": 1.0,
            "LABEL": "Mouse X"
        },
        {
            "NAME": "mouseY",
            "TYPE": "float",
            "DEFAULT": 0.5,
            "MIN": 0.0,
            "MAX": 1.0,
            "LABEL": "Mouse Y"
        }
    ],
    "TAGS": [
        "grid"
    ]
}*/




#define time ((useFrameIndex ? (float(FRAMEINDEX) / fps) : TIME) * timeScale)
#define mouse vec2(mouseX, mouseY)
#define resolution RENDERSIZE
#ifdef GL_ES
precision mediump float;
#endif

const float pi = 3.14159;

vec3 rotate(vec3 v,vec2 r) 
{
	mat3 rxmat = mat3(1,   0    ,    0    ,
			  0,cos(r.y),-sin(r.y),
			  0,sin(r.y), cos(r.y));
	mat3 rymat = mat3(cos(r.x), 0,-sin(r.x),
			     0    , 1,    0    ,
			  sin(r.x), 0,cos(r.x));

	return (v*rxmat)*rymat;
	
}

vec3 norm(vec3 v)
{
	//box made of 6 planes
	float tp = dot(v,vec3(0,-1,0))*3.;
	float bt = dot(v,vec3(0,1,0))*3.;
	float lf = dot(v,vec3(1,0,0))*3.;
	float rt = dot(v,vec3(-1,0,0))*3.;
	float fr = dot(v,vec3(0,0,1))*3.;
	float bk = dot(v,vec3(0,0,-1))*3.;
	
	return v/min(min(min(min(min(tp,bt),lf),rt),fr),bk);
}

float grid(vec3 v)
{
	float g;
	g = abs((mod(v.x*4.,0.25)*4.)-0.5);
	g = max(g,abs((mod(v.y*4.,0.25)*4.)-0.5));
	g = max(g,abs((mod(v.z*4.,0.25)*4.)-0.5));
	
	g = smoothstep(0.5+length(v)*0.25,0.5,1.-g);
	return g;
}

void main( void ) {

	vec2 res = vec2(resolution.x/resolution.y,1.0);
	vec2 p = ( gl_FragCoord.xy / resolution.y ) -(res/2.0);
	
	vec2 m = (mouse-0.5)*pi*vec2(2.,1.);
	
	vec3 color = vec3(0.0);
	
	vec3 pos = norm(rotate(vec3(p,0.5),vec2(m)));
	
	color = vec3(grid(pos));
		
	gl_FragColor = vec4(  color , 1.0 );

}


